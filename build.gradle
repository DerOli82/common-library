buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }    
    }
    dependencies {
        classpath "org.eclipse.jgit:org.eclipse.jgit:4.0.0.201505050340-m2"
        classpath "net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT"
        classpath "org.ajoberstar:gradle-git:1.6.0"
    }
}
apply plugin: "forge"
apply plugin: "org.ajoberstar.grgit"

//Load properties
file "build.properties" withReader {
    def prop = new Properties()
    prop.load(it)
    ext.config = new ConfigSlurper().parse prop
}

//Get Github commit hash
task tagRelease {
  description = 'Tags the current head with the project\'s version.'
  doLast {
    grgit.tag.add {
      name = version
      message = "Release of ${version}"
    }
  }
}

//Version
version = "${config.minecraft.version}-${config.mod.version}-${grgit.head().abbreviatedId}"
group= "de.alaoli.games.minecraft.mods.lib.common" 
archivesBaseName = "olis-common-library"

sourceCompatibility = org.gradle.api.JavaVersion.VERSION_1_8
targetCompatibility = org.gradle.api.JavaVersion.VERSION_1_8

configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    shade "com.eclipsesource.minimal-json:minimal-json:0.9.+"
}

minecraft {
    mappings = "stable_12"
    version = "${config.forge.version}"
    runDir = "run"
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", "${config.yadm.version}"
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"
                
        // replace version and mcversion
        expand "version":"${config.yadm.version}", "mcversion":project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }
}

jar {
    version = project.version
    configurations.shade.copyRecursive().setTransitive(false).each { artifact -> from (zipTree(artifact)) }
}
